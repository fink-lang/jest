{new} = import '@fink/js-interop/reflect'

{String, Number, Error} = import '@fink/js-interop/globals'


{skip, only, describe, it, expect, any, mock, mock_fn} = import '.'
{to_equal, to_not_equal} = import '.'
{was_called, was_not_called, was_called_with, was_not_called_with} = import '.'
{to_throw, to_not_throw} = import '.'


mock_test = mock_fn fn: 'testing'

mock '@fink/test'
  fn: dict:
    test: mock_test
  {virtual: true}


{test} = import '@fink/test'


only.describe 'test utils', fn:

  only.it 'equality', fn:
    expect
      'foobar'
      to_equal 'foobar'

    expect
      'foobar'
      to_not_equal 'spam'


  only.it 'tests mock funcs', fn:
    foo = mock_fn fn: false

    expect
      foo
      was_not_called

    foo 'bar'

    expect
      foo
      was_called

    expect
      foo
      was_called_with any String

    expect
      foo
      was_not_called_with any Number

    expect
      foo
      was_called_with 'bar'


  only.it 'test mock module virtual', fn:
    test 'testing'
    expect
      mock_test
      was_called_with 'testing'


  only.it 'tests exceptions', fn:
    foo = mock_fn
      # istanbul ignore next
      fn msg:
        err = new Error, msg
        throw err

    expect
      fn: foo 'test'
      to_throw 'test'

    expect
      fn: 'test'
      to_not_throw ()

  skip.it 'skips',
    expect
      true
      to_equal false


describe 'plain describe', fn:
  it 'ignores return value', fn:
    'foo'

  'ignored too'


skip.describe 'test utils', fn:
  'foo'